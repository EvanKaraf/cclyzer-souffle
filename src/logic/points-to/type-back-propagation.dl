.decl heap_allocation_by_type_instruction(?type: Type, ?insn: Instruction, ?heapAlloc: HeapAllocation)

// Mark all heap allocations that do not carry any type information

.decl untyped_allocation(?insn: Instruction, ?alloc: Allocation)

untyped_allocation(?insn, ?alloc) :-
   heap_allocation_by_instruction(?insn, ?alloc).


// Type indication for *untyped* allocation

.decl type_indication(?type: Type, ?actx: AContext, ?alloc: Allocation)
.decl type_indication1(?type: Type, ?actx: AContext, ?alloc: Allocation)
.decl type_indication2(?type: Type, ?actx: AContext, ?alloc: Allocation)
.decl type_indication3(?type: Type, ?actx: AContext, ?alloc: Allocation)

.output type_indication1
.output type_indication2
.output type_indication3

// Only bitcast instructions are considered type indications for now
type_indication1(?type, ?actx, ?alloc),
type_indication(?type, ?actx, ?alloc) :-
   bitcast_instruction_to_type(?instr,?ptrType),
   bitcast_instruction_from_operand(?instr,?fromOperand),
   instruction_in_function(?instr,?instrFunc),
   reachable_context(?ctx, ?instrFunc),
   operand_points_to(?actx, ?alloc, ?ctx, ?fromOperand),
   untyped_allocation(?allocInstr, ?alloc),
   !sized_alloc_instr(?allocInstr,_),
   pointer_type_has_component(?ptrType,?type).

type_indication2(?type, ?actx, ?alloc),
type_indication(?type, ?actx, ?alloc) :-
   reachable_instruction(?instr),
   bitcast_instruction_to_type(?instr,?ptrType),
   bitcast_instruction_from_operand(?instr,?fromOperand),
   instruction_in_function(?instr,?instrFunc),
   reachable_context(?ctx, ?instrFunc),
   operand_points_to(?actx, ?alloc, ?ctx, ?fromOperand),
   untyped_allocation(?allocInstr, ?alloc),
   pointer_type_has_component(?ptrType,?type),
   sized_alloc_instr(?allocInstr,?size),
   type_has_size(?type,?size).

// In case of known size, we must also allow pointer types whose base
// component may perfectly divide the known size of the allocation.
type_indication3(?type, ?actx, ?alloc),
type_indication(?type, ?actx, ?alloc) :-
   reachable_instruction(?instr),
   bitcast_instruction_to_type(?instr,?ptrType),
   bitcast_instruction_from_operand(?instr,?fromOperand),
   instruction_in_function(?instr,?instrFunc),
   reachable_context(?ctx, ?instrFunc),
   operand_points_to(?actx, ?alloc, ?ctx, ?fromOperand),
   untyped_allocation(?allocInstr, ?alloc),
   pointer_type_has_component(?ptrType,?type),
   pointer_type_base_component(?ptrType, ?baseType),
   sized_alloc_instr(?allocInstr, size),
   type_has_size(?baseType,baseSize),
   size = (size / baseSize) * baseSize.

heap_allocation(?typedAlloc):-
   heap_allocation_by_type_instruction(_, _, ?typedAlloc).

.decl _assign_rebase_alloc(?typedAlloc: HeapAllocation, ?actx: AContext, ?alloc: Allocation, ?var: Variable)
heap_allocation_by_type_instruction(?type, ?allocInstr, ?typedAlloc), 
   _assign_rebase_alloc(?typedAlloc, ?actx, ?alloc, ?var)
 :-
   untyped_allocation(?allocInstr, ?alloc),
   type_indication(?type, ?actx, ?alloc),
   instruction_assigns_to(?allocInstr, ?var),
   variable_in_function(?var, ?func),
   variable_has_name(?var, ?name),
   function_has_name(?func, ?funcName),
   ?typedAlloc= cat(cat(cat(cat(cat(cat("*typed_heap_alloc", ?funcName), "[" ), ?type ), " " ), ?name ), "]").

.output _assign_rebase_alloc

var_points_to(?actx, ?typedAlloc, ?ctx, ?var) :-
   _assign_rebase_alloc(?typedAlloc, ?actx, ?alloc, ?var),
   var_points_to(?actx, ?alloc, ?ctx, ?var).

allocation_type(?alloc, ?type) :-
   heap_allocation_by_type_instruction(?type, _, ?alloc).

allocation_to_string(?alloc, ?str) :-
   heap_allocation_by_type_instruction(_, _, ?alloc),
   ?str = ?alloc.

allocation_pos(?alloc, ?line, ?column) :-
   heap_allocation_by_type_instruction(_, ?insn, ?alloc),
   instruction_pos(?insn, ?line, ?column).

