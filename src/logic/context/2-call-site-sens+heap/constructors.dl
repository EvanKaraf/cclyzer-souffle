//------------------------------------------------------------------------------
// [2 Call-site Sensitive + Heap Analysis]
//
// In this analysis, context is the cons of the latest 2 invocation
// instructions, whereas the allocation context consists of just the
// latest invocation instruction.
//------------------------------------------------------------------------------

.type Context = [?item0: ContextItem, ?item1: ContextItem]
.type AllocationContext = ContextItem
.type ContextItem = Instruction | symbol


.decl context(?ctx:Context)
.decl alloc_context(?allocCtx:AllocationContext)
.decl context_item(?item:ContextItem)

//-----------------------------------------------------
// Context Constructors
//-----------------------------------------------------


.decl context_new(?item: ContextItem, ?ctx:Context)
.decl alloc_context_new(?item: ContextItem, ?aCtx:AllocationContext)


.decl context_request(?callerCtx: Context, ?aCtx: AllocationContext, ?invoc: ContextItem)
.decl context_response(?callerCtx: Context, ?aCtx: AllocationContext, ?invoc: ContextItem, ?ctx: Context)

context(?ctx),
context_response(?callerCtx, ?aCtx, ?invoc, ?ctx) :-
   context_request(?callerCtx, ?aCtx, ?invoc),
   context_item(?item0),
   ?callerCtx=[?item1, ?item0],
   ?ctx = [?item0, ?invoc].


.decl alloc_context_request(?invoc: ContextItem)
.decl alloc_context_response(?invoc: ContextItem, ?ctx: AllocationContext)

alloc_context_response(?invoc, ?aCtx) :-
   alloc_context_request(?invoc),
   ?aCtx=?invoc.


//-----------------------------------------------------
// Context Item Constructors
//-----------------------------------------------------

.decl context_item_by_invoc(?invoc:Instruction, ?ctxItem:ContextItem)
.decl context_item_invoc(?ctxItem:ContextItem, ?invoc:Instruction)

context_item_invoc(?ctxItem, ?invoc) :-
   context_item_by_invoc(?invoc, ?ctxItem).

context_item(?instr)
 , context_item_by_invoc(?instr, ?instr):-
   (call_instruction(?instr) ;
   invoke_instruction(?instr)),
   ?instr = ?instr.


// Dummy context items (needed for empty contexts)
.decl matches_string_context(?str:symbol)

matches_string_context(?str):-
   ?str = "<<empty-context>>";
   ?str = "<<main-context>>";
   ?str = "<<startup-context>>";
   ?str = "<<exit-context>>".

.decl dummy_context_item_by_str(?str:symbol,?ctxItem:ContextItem)
context_item(?str)
 , dummy_context_item_by_str(?str,?str)
 :-
   matches_string_context(?str).

//-----------------------------------------------------
// Macro-constructors
//-----------------------------------------------------
.decl record(?ctx: Context, ?alloc: Allocation, ?aCtx: AllocationContext)

.decl merge(?callerCtx: Context, ?invoc: Instruction, ?aCtx: AllocationContext, ?alloc: Allocation, ?calleeCtx: Context)


// // Macro-definitions

alloc_context_new(?item, ?aCtx) :-
   record(?ctx, _, ?aCtx),
   context_new(?item, ?ctx).


context_new(?newItem, ?calleeCtx) :-
   merge(?callerCtx, ?invoc, _, _, ?calleeCtx),
   context_new(_,?callerCtx),
   context_item_by_invoc(?invoc, ?newItem).

//-----------------------------------------------------
// Unpacking context items
//-----------------------------------------------------


.decl context_getitem0(?ctx:Context, ?item:ContextItem)
context_getitem0(?ctx, ?item):-
   context_item(?item),
   context_response(?a,?b,?c,?ctx), context_response(?a,?b,?c,[_,?item]).
//   context_new([_, ?item], ?ctx).

.decl context_getitem1(?ctx:Context, ?item:ContextItem)
context_getitem1(?ctx, ?item):-
   context_item(?item),
   context_response(?a,?b,?c,?ctx), context_response(?a,?b,?c,[?item,_]).
//   context_new([?item, _], ?ctx).

.decl context_getinvoc0(?ctx:Context, ?item:ContextItem)
context_getinvoc0(?ctx, ?invoc):-
   context_getitem0(?ctx, ?item),
   context_item_invoc(?item, ?invoc).

.decl context_getinvoc1(?ctx:Context, ?item:ContextItem)
context_getinvoc1(?ctx, ?invoc):-
   context_getitem1(?ctx, ?item),
   context_item_invoc(?item, ?invoc).

.decl alloc_context_getitem(?aCtx:AllocationContext, ?item:ContextItem)
alloc_context_getitem(?aCtx, ?item):-
   alloc_context_new(?item, ?aCtx).

.decl alloc_context_getinvoc(?aCtx:AllocationContext, ?invoc:Instruction)
alloc_context_getinvoc(?aCtx, ?invoc) :-
   alloc_context_getitem(?aCtx, ?item),
   context_item_invoc(?item, ?invoc).

//Already generated output relations of 1-call-sensitivity
.decl context_getinvoc(?dummy : number)
.decl context_getitem(?ctx: Context, ?ctxItem: ContextItem)

context_getitem(?ctx, ?ctxItem) :-
   context_response(_, _, ?ctxItem, ?ctx).
